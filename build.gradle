subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'maven'

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    group = "com.ifountain.opsgenieclient"
    version = "2.0.0-SNAPSHOT"

    repositories {
        mavenCentral()
        maven {
            url "http://repo.opsgenie.com:9393/content/groups/public/"
        }
    }

    test {
        ignoreFailures = true
        workingDir = project.rootProject.rootDir
        filter {
            exclude "**/**TestCase**"
        }
        testLogging.showStandardStreams = true
    }

    jar {
        manifest {
            attributes("Built-By": "http://www.opsgenie.com",
                    "Specification-Title": project.name[0].toUpperCase() + project.name[1..-1],
                    "Specification-Product": project.name,
                    "Specification-Version": project.version,
                    "Specification-Vendor": "http://www.opsgenie.com",
                    "Implementation-Title" : project.name[0].toUpperCase() + project.name[1..-1],
                    "Implementation-Product": project.name,
                    "Implementation-Vendor":"http://www.opsgenie.com"
            )
        }
    }

    task listJars << {
        configurations.compile.sort().each { File file -> println file.name }
    }

    dependencies {
        compile 'log4j:log4j:1.2.16'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2'
}